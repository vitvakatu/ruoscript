digit = {
    '0'..'9'
}

char = {
    'a'..'z' | 'A'..'Z'
}

identifier = @{
    (char | "_") ~ (char | digit | "_")*
}

opt_sign = _{
    ("+" | "-")?
}

int = @{
    opt_sign ~ digit+
}

float = @{
    opt_sign ~ digit+ ~ "." ~ digit+ ~ (^"e" ~ opt_sign ~ digit+)?
}

bin_op = _{
    op_add | op_sub | op_mul | op_div | op_pow |
    op_eq | op_neq | op_ge | op_gt | op_le | op_lt |
    op_and | op_or
}
op_add = { "+" }
op_sub = { "-" }
op_mul = { "*" }
op_div = { "/" }
op_pow = { "^" }
op_eq = { "==" }
op_neq = { "!=" }
op_ge = { ">=" }
op_gt = { ">" }
op_le = { "<=" }
op_lt = { "<" }
op_and = { "&&" | "and" }
op_or = { "||" | "or" }

un_op = _{
    op_not | op_minus
}
op_not = { "!" | "not" }
op_minus = { "-" }

bin_expr = {
    term ~ ( bin_op ~ term)*
}
term = _{
    primary | (lparen ~ bin_expr ~ rparen)
}
primary = _{
    un_op? ~ (identifier | float | int)
}
lparen = _{ "(" }
rparen = _{ ")" }

program = {
    soi ~ bin_expr ~ eoi
}

whitespace = _{ " " | "\t" }
